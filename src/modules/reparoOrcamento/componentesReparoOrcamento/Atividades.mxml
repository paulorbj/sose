<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%"
		 xmlns:combos="components.combos.*" xmlns:grid="components.grid.*"
		 xmlns:texto="components.textInput.*" xmlns:componentes="components.*"
		 paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10"
		 initialize="init()">
	<fx:Declarations>
		<mx:RemoteObject concurrency="single" showBusyCursor="true" id="atividadeOrcRepService" destination="atividadeOrcRepService" endpoint="{resourceManager.getString('ConfigurationResource','amf')}">			
			<mx:method name="salvarAtividadeOrcRep"      
					   fault="onSalvarAtividadeOrcRepFault(event)" 
					   result="onSalvarAtividadeOrcRepResult(event)"
					   />			
			<mx:method name="excluirAtividadeOrcRep"      
					   fault="onExcluirAtividadeOrcRepFault(event)" 
					   result="onExcluirAtividadeOrcRepResult(event)"
					   />
		</mx:RemoteObject>
	</fx:Declarations>
	
	
	<fx:Script>
		<![CDATA[
			import components.PopupModalController;
			import components.UserRegisterNumberModal;
			import components.events.UserRegisterNumberModalEvent;
			import components.messages.MensagemEvent;
			import components.messages.MessageType;
			
			import entities.administrativo.Usuario;
			import entities.orcamentoreparo.AtividadeOrcRep;
			import entities.orcamentoreparo.Orcamento;
			import entities.orcamentoreparo.Reparo;
			
			import modules.view.Login;
			
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			import mx.managers.CursorManager;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.validators.Validator;
			
			[Bindable]
			private var _atividades:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			private var _reparo:Reparo;
			
			[Bindable]
			private var _orcamento:Orcamento;
			
			[Bindable]
			private var _itensParaDeletar:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			private var _atividade:AtividadeOrcRep = new AtividadeOrcRep();
			
			[Bindable]
			private var validatorArr:Array;
			
			private var indexEdicao:int = -1;
			
			public function getAtividades():ArrayCollection
			{
				return _atividades;
			}
			
			public function setAtividades(atividades:ArrayCollection,reparo:Reparo,orcamento:Orcamento):void
			{
				if(atividades == null){
					atividades = new ArrayCollection();
				}
				_atividades = atividades;	
				_reparo = reparo;
				_orcamento = orcamento;
			}
			
			public function init():void
			{
				validatorArr = new Array();
				validatorArr.push(cbAtividade.comboBox.comboValidator);
			}
			
			public function adicionar(event:MouseEvent = null):void
			{
				var usr:Usuario = Login.usuarioAutenticado;
				if(usr){
					if(_reparo != null ? usr.id == _reparo.tecnicoResponsavel.id : usr.id == _orcamento.tecnicoResponsavel.id){
						if(validateAdicinarAtividade()){
							_atividade.atividade = cbAtividade.atividadeSelecionada;
							_atividade.usuario = Login.usuarioAutenticado;
							if(_reparo){
								_atividade.reparo = _reparo;
							}else{
								_atividade.orcamento = _orcamento;
							}
							atividadeOrcRepService.salvarAtividadeOrcRep(_atividade);
						}
					}else{
						dispatchEvent(new MensagemEvent("Usuário não é o técnico responsável!", MessageType.WARNING, 3000));
						Login.usuarioAutenticado = null;
					}
				}else{
					PopupModalController.getInstance().showModalAutenticacaoUsuario(onAdicionarAutenticar);
				}
				
			}
			
			
			public function onAdicionarAutenticar(event:UserRegisterNumberModalEvent=null):void{
				adicionar();
			}
			
			private function onSalvarAtividadeOrcRepResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				
				if(_atividade.id == 0){
					_atividade = event.result as AtividadeOrcRep;
					_atividades.addItem(_atividade);
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ATIVIDADE_SALVAR_SUCESSO'), MessageType.SUCCESS, 5000));
				}else{
					var index:int = _atividades.getItemIndex(_atividade);
					_atividades.removeItemAt(index);
					_atividade = event.result as AtividadeOrcRep;
					_atividades.addItemAt(_atividade, index);
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ATIVIDADE_EDITAR_SUCESSO'), MessageType.SUCCESS, 5000));
				}
				limpar();
			}
			
			private function onSalvarAtividadeOrcRepFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[onListarHistoricoOrcRepFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");
				dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ATIVIDADE_EDITAR_ERRO'), MessageType.ERROR, 5000));
			}
			
			public function atualizarCampos(event:Event):void
			{
				if(cbAtividade.atividadeSelecionada != null){
					_atividade.atividade = cbAtividade.atividadeSelecionada;
				}
			}
			
			private function validateAdicinarAtividade():Boolean {	
				var validatorErrorArray:Array = Validator.validateAll(validatorArr);;
				var isValidForm:Boolean = validatorErrorArray.length == 0;
				return isValidForm;
			}
			
			public function editAtividade(data:Object, event:Event=null):void
			{					
				if(this.currentState == "editar"){
					//for double click
					if(data==null && event is ListEvent){
						var event2:ListEvent = event as ListEvent						
						data = _atividades.getItemAt(event2.rowIndex);				
					}	
					_atividade = data as AtividadeOrcRep;
					cbAtividade.atividadeSelecionada = _atividade.atividade;
					indexEdicao = _atividades.getItemIndex(_atividade);
					
				}
			}
			
			public function excluirAtividade(data:Object, event:Event=null):void
			{		
				if(this.currentState == "editar"){
					_atividade = data as AtividadeOrcRep;
					var index:int = _atividades.getItemIndex(_atividade);
					if(_atividade != null && _atividade.id == 0){
						_atividades.removeItemAt(index);
					}else{
						atividadeOrcRepService.excluirAtividadeOrcRep(_atividade);
					}
				}
			}
			
			private function onExcluirAtividadeOrcRepResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				
				var index:int = _atividades.getItemIndex(_atividade);
				_atividades.removeItemAt(index);
				_atividade = event.result as AtividadeOrcRep;
				dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ATIVIDADE_EDITAR_SUCESSO'), MessageType.SUCCESS, 5000));
				limpar();
			}
			
			private function onExcluirAtividadeOrcRepFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[onExcluirAtividadeOrcRepFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");
				dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ATIVIDADE_EDITAR_ERRO'), MessageType.ERROR, 5000));
			}
			
			public function limpar():void
			{
				_atividade = new AtividadeOrcRep();
				cbAtividade.limpar();
				indexEdicao = -1;
				txtJustificativa.inputText = "";

			}
			
		]]>
	</fx:Script>
	
	<mx:states>
		<s:State name="editar"/>
		<s:State name="visualizar"/>
	</mx:states>
	
	<fx:Binding destination="txtDescricao.input.text" source="_atividade.atividade.descricao" twoWay="true"/>
	<fx:Binding destination="txtJustificativa.input.text" source="_atividade.justificativa" twoWay="true"/>
	
	<mx:HBox width="100%">
		<combos:ComboAtividade id="cbAtividade" enabled.visualizar="false" required="true" labelText="{resourceManager.getString('ApplicationResource','ATIVIDADES')}" width="20%" changeFunction="atualizarCampos" />
		<texto:TextInput id="txtDescricao"  labelText="{resourceManager.getString('ApplicationResource','DESCRICAO')}" width="40%" enabled="false" />
	</mx:HBox>
	<mx:HBox verticalAlign="bottom" width="100%">
		<componentes:TextAreaInput id="txtJustificativa" enabled.visualizar="false" width="50%" heightInLines="2" labelText="{resourceManager.getString('ApplicationResource','JUSTIFICATIVA')}" />
		<mx:Button id="btAdicionar" enabled.visualizar="false"
				   label="{resourceManager.getString('ApplicationResource','ADICIONAR')}"
				   height="32"  width="10%"
				   icon="@Embed(source='/assets/icons/add.png')"
				   click="adicionar(event)"
				   />
	</mx:HBox>
	
	<grid:MantisTable id="viewTable"
					  width="770" height="100%"
					  draggableColumns="true"
					  dataProvider="{_atividades}"
					  itemDoubleClick="editAtividade(data, event)"	>
		<grid:columns>
			
			<!-- EDIT/REMOVE COLUMNS -->
			
			<mx:AdvancedDataGridColumn headerText = "{resourceManager.getString('ApplicationResource','ACTION_COLUMN')}"	
							   draggable="false" width="50"								 
							   editable="false"
							   sortable="false">
				<mx:itemRenderer>
					<fx:Component>
						<mx:HBox horizontalAlign="center"
								 verticalAlign="middle"
								 >
							
							<mx:Image source="@Embed(source='/assets/icons/edit.png')" id="btEditar"
									  click="parentDocument.editAtividade(data, event)"
									  buttonMode="true"
									  useHandCursor="true"
									  toolTip="{resourceManager.getString('ApplicationResource','EDIT')}"
									  />
							
							<mx:Image source="@Embed(source='/assets/icons/delete.png')" id="btExcluir"
									  click="parentDocument.excluirAtividade(data, event)"
									  buttonMode="true" visible="{parentDocument._reparo != null ? parentDocument._reparo.propostaReprovada == null : parentDocument._orcamento.propostaReprovada}"
									  useHandCursor="true"
									  toolTip="{resourceManager.getString('ApplicationResource','EXCLUIR')}"
									  />
							
						</mx:HBox>
					</fx:Component>
				</mx:itemRenderer>
			</mx:AdvancedDataGridColumn>
						<mx:AdvancedDataGridColumn id="cAtividade" width="150"
							   headerText = "{resourceManager.getString('ApplicationResource','ATIVIDADE')}"
							   textAlign="center"
							   sortable="false"
							   dataField="atividade.nome"							   
							   />
			
			<mx:AdvancedDataGridColumn id="cJustificativa" dataField="justificativa" width="300"
							   headerText = "{resourceManager.getString('ApplicationResource','JUSTIFICATIVA')}"
							   textAlign="center"
							   sortable="false"					   
							   />
			
			<mx:AdvancedDataGridColumn id="cDtInclusao" width="100" textAlign="center"
							   headerText = "{resourceManager.getString('ApplicationResource','DATA')}"
							   dataField = "data"
							   />
			
			<mx:AdvancedDataGridColumn id="cUsuario" width="100" textAlign="center"
							   headerText = "{resourceManager.getString('ApplicationResource','TECNICO')}"
							   dataField = "usuario.usuario"
							   />
			<!-- CHANGE HERE THE ENTITY COLUMNS -->
			
		</grid:columns>
	</grid:MantisTable>
	
</mx:VBox>
