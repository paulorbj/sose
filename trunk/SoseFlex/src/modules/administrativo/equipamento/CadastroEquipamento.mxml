<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:equipamento="modules.administrativo.equipamento.*"
		 creationComplete="onCreationComplete()"
		 width="100%" horizontalAlign="center" height="100%" 
		 paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10" >
	
	<fx:Declarations>
		<mx:RemoteObject id="equipamentoService" destination="equipamentoService" endpoint="{resourceManager.getString('ConfigurationResource','amf')}">			
			<mx:method name="listarEquipamentos"      
					   fault="onListarEquipamentosFault(event)" 
					   result="onListarEquipamentosResult(event)"
					   />
			<mx:method name="salvarEquipamento"      
					   fault="onSalvarEquipamentoFault(event)" 
					   result="onSalvarEquipamentoResult(event)"
					   />
			<mx:method name="excluirEquipamento"      
					   fault="onExcluirEquipamentoFault(event)" 
					   result="onExcluirEquipamentoResult(event)"
					   />
		</mx:RemoteObject>
		
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import components.messages.MensagemEvent;
			import components.messages.MessageType;
			
			import entities.administrativo.Equipamento;
			
			import modules.administrativo.equipamento.eventos.EquipamentoEvent;
			
			import mx.collections.ArrayCollection;
			import mx.managers.CursorManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]			
			private var _equipamento:Equipamento;
			
			[Bindable]	
			private var _listaEquipamento:ArrayCollection = new ArrayCollection;
			
			private function onCreationComplete():void
			{
				equipamentoService.listarEquipamentos();	
				this.addEventListener(EquipamentoEvent.EDITAR_EQUIPAMENTO,onEditar);
				this.addEventListener(EquipamentoEvent.EXCLUIR_EQUIPAMENTO,onExcluir);
			}
			
			private function onEditar(event:EquipamentoEvent):void
			{
				this.currentState = "edicao";
				dados.equipamento = event.equipamento;
			}
			
			private function onExcluir(event:EquipamentoEvent):void
			{
				_equipamento = event.equipamento;
				equipamentoService.excluirEquipamento(event.equipamento);
			}
			
			private function onListarEquipamentosResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				_listaEquipamento.removeAll();
				_listaEquipamento = event.result as ArrayCollection;
				listagem.setListaEquipamento(_listaEquipamento);
			}
			
			private function onListarEquipamentosFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[ListarEquipamentosFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");		
			}
			
			private function onSalvarEquipamentoResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				_equipamento = event.result as Equipamento;
				if(dados.equipamento.id == 0){
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','EQUIPAMENTO_SALVAR_SUCESSO'), MessageType.SUCCESS, 10000));
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','EQUIPAMENTO_EDITAR_SUCESSO'), MessageType.SUCCESS, 10000));
				}
				listagem.adicionarEquipamento(_equipamento);
				dados.limpar();
			}
			
			private function onSalvarEquipamentoFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[SalvarEQUIPAMENTOFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");
				var str:String = event.fault.faultString.substr(0,22);
				var i:Number;
				if(str == "br.com.sose.exceptions"){
					i = event.fault.faultString.indexOf(":");
					dispatchEvent(new MensagemEvent(event.fault.faultString.substring(i+1), MessageType.ERROR, 10000));
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','EQUIPAMENTO_SALVAR_ERRO'), MessageType.ERROR, 10000));	
				}		
			}
			
			private function onExcluirEquipamentoResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				var index:int = _listaEquipamento.getItemIndex(_equipamento);
				_listaEquipamento.removeItemAt(index);
				listagem.setListaEquipamento(_listaEquipamento);
				limpar();
				dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','EQUIPAMENTO_REMOVER_SUCESSO'), MessageType.SUCCESS, 10000));
			}
			
			private function onExcluirEquipamentoFault(event:FaultEvent = null):void
			{				
				CursorManager.removeAllCursors();
				trace("[ExcluirEquipamentoFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");
				var i:Number;
				if(event.fault.faultString.search("br.com.sose.exceptions") != -1){
					i = event.fault.faultString.indexOf(":");
					dispatchEvent(new MensagemEvent(event.fault.faultString.substring(i+1), MessageType.ERROR, 10000));
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','EQUIPAMENTO_REMOVER_ERRO'), MessageType.ERROR, 10000));
				}
			}
			
			private function salvar(event:MouseEvent = null):void
			{
				if(dados.validarCampos()){
					equipamentoService.salvarEquipamento(dados.equipamento);
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ERRO_VALIDACAO'), MessageType.WARNING, 10000));
				}
			}
			
			private function limpar(event:MouseEvent = null):void
			{
				_equipamento = new Equipamento();
				if(dados)
					dados.limpar();
			}
			
			private function novo(event:MouseEvent = null):void
			{
				this.currentState = "insercao";
				limpar();			
			}
			
			private function voltar(event:MouseEvent = null):void
			{
				this.currentState = "listagem";
				limpar();
			}
			
			private function salvarEnter(event:KeyboardEvent):void
			{		
				if (event.charCode == 13) {
					salvar();
				}
			}
			
			private function limparEnter(event:KeyboardEvent):void
			{		
				if (event.charCode == 13) {
					limpar();
				}
			}
			
			private function voltarEnter(event:KeyboardEvent):void
			{		
				if (event.charCode == 13) {
					voltar();
				}
			}
		]]>
	</fx:Script>
	
	<mx:states>
		<s:State name="listagem"/>
		<s:State name="edicao"/>
		<s:State name="insercao"/>
	</mx:states>
	
	<mx:VBox width="100%" horizontalAlign="center">
		<s:Label fontSize="15" fontWeight="bold" text="{resourceManager.getString('ApplicationResource','LISTA_EQUIPAMENTO')}" includeIn="listagem"/>
		<s:Label fontSize="15" fontWeight="bold" text="{resourceManager.getString('ApplicationResource','DETALHAMENTO_EQUIPAMENTO')}" excludeFrom="listagem"/>
	</mx:VBox>	
	
	<mx:VBox width="100%" horizontalAlign="left" >
		<mx:Button id="newButton" includeIn="listagem"
				   label="{resourceManager.getString('ApplicationResource','NOVO')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/new.png')"
				   click="novo(event)"
				   />
	</mx:VBox>
	
	<equipamento:DadosEquipamento id="dados" excludeFrom="listagem" width="100%" />
	
	<equipamento:ListagemEquipamentoCadastrado id="listagem" includeIn="listagem" width="100%" />
	
	<mx:HBox width="100%" excludeFrom="listagem">
		<mx:Button id="saveButton" excludeFrom="listagem"
				   label="{resourceManager.getString('ApplicationResource','SALVAR')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/save.png')"
				   keyDown="salvarEnter(event)"
				   click="salvar(event)"
				   />
		<mx:Button id="cleanButton" excludeFrom="listagem"
				   label="{resourceManager.getString('ApplicationResource','LIMPAR')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/clean.png')"
				   keyDown="limparEnter(event)"
				   click="limpar(event)"
				   />
		<mx:Button id="voltarButton" excludeFrom="listagem"
				   label="{resourceManager.getString('ApplicationResource','VOLTAR')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/back.png')"
				   keyDown="voltarEnter(event)"
				   click="voltar(event)"
				   />
	</mx:HBox>
</mx:VBox>
