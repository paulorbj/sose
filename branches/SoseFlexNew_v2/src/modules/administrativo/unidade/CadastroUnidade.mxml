<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:unidade="modules.administrativo.unidade.*"
		 creationComplete="onCreationComplete()"
		 width="100%" horizontalAlign="center" height="100%" 
		 paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10" >
	
	<fx:Declarations>
		<mx:RemoteObject id="unidadeService" destination="unidadeService" endpoint="{resourceManager.getString('ConfigurationResource','amf')}">			
			<mx:method name="listarUnidades"      
					   fault="onListarUnidadesFault(event)" 
					   result="onListarUnidadesResult(event)"
					   />
			<mx:method name="salvarUnidade"      
					   fault="onSalvarUnidadeFault(event)" 
					   result="onSalvarUnidadeResult(event)"
					   />
			<mx:method name="excluirUnidade"      
					   fault="onExcluirUnidadeFault(event)" 
					   result="onExcluirUnidadeResult(event)"
					   />
		</mx:RemoteObject>
		
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import components.messages.MensagemEvent;
			import components.messages.MessageType;
			
			import entities.administrativo.Unidade;
			
			import modules.administrativo.unidade.eventos.UnidadeEvent;
			
			import mx.collections.ArrayCollection;
			import mx.managers.CursorManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]			
			private var _unidade:Unidade;
			
			[Bindable]	
			private var _listaUnidade:ArrayCollection = new ArrayCollection;
			
			private function onCreationComplete():void
			{
				unidadeService.listarUnidades();	
				this.addEventListener(UnidadeEvent.EDITAR_UNIDADE,onEditar);
				this.addEventListener(UnidadeEvent.EXCLUIR_UNIDADE,onExcluir);
			}
			
			private function onEditar(event:UnidadeEvent):void
			{
				this.currentState = "edicao";
				dados.unidade = event.unidade;
			}
			
			private function onExcluir(event:UnidadeEvent):void
			{
				_unidade = event.unidade;
				unidadeService.excluirUnidade(event.unidade);
			}
			
			private function onListarUnidadesResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				_listaUnidade.removeAll();
				_listaUnidade = event.result as ArrayCollection;
				listagem.setListaUnidade(_listaUnidade);
			}
			
			private function onListarUnidadesFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[ListarUnidadesFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");		
			}
			
			private function onSalvarUnidadeResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				_unidade = event.result as Unidade;
				if(dados.unidade.id == 0){
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','UNIDADE_SALVAR_SUCESSO'), MessageType.SUCCESS, 10000));
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','UNIDADE_EDITAR_SUCESSO'), MessageType.SUCCESS, 10000));
				}
				listagem.adicionarUnidade(_unidade)
				dados.limpar();
			}
			
			private function onSalvarUnidadeFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[SalvarAtividadeFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");
				var str:String = event.fault.faultString.substr(0,22);
				var i:Number;
				if(str == "br.com.sose.exceptions"){
					i = event.fault.faultString.indexOf(":");
					dispatchEvent(new MensagemEvent(event.fault.faultString.substring(i+1), MessageType.ERROR, 10000));
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','UNIDADE_SALVAR_ERRO'), MessageType.ERROR, 10000));	
				}			
			}
			
			private function onExcluirUnidadeResult(event:ResultEvent = null):void
			{
				CursorManager.removeAllCursors(); 
				var index:int = _listaUnidade.getItemIndex(_unidade);
				_listaUnidade.removeItemAt(index);
				listagem.setListaUnidade(_listaUnidade);
				limpar();
				dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','UNIDADE_REMOVER_SUCESSO'), MessageType.SUCCESS, 10000));
				
			}
			
			private function onExcluirUnidadeFault(event:FaultEvent = null):void
			{
				CursorManager.removeAllCursors();
				trace("[ExcluirAtividadeFault] [" +	event.fault.faultCode + "] " + event.fault.faultString + " (" +	event.fault.faultDetail + ")");
				var i:Number;
				if(event.fault.faultString.search("br.com.sose.exceptions") != -1){
					i = event.fault.faultString.indexOf(":");
					dispatchEvent(new MensagemEvent(event.fault.faultString.substring(i+1), MessageType.ERROR, 10000));
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','UNIDADE_SALVAR_ERRO'), MessageType.ERROR, 10000));	
				}			
			}
			
			private function salvar(event:MouseEvent = null):void
			{
				if(dados.validarCampos()){
					unidadeService.salvarUnidade(dados.unidade);
				}else{
					dispatchEvent(new MensagemEvent(resourceManager.getString('ApplicationResource','ERRO_VALIDACAO'), MessageType.WARNING, 10000));
				}
			}
			
			private function limpar(event:MouseEvent = null):void
			{
				_unidade = new Unidade();
				if(dados)
					dados.limpar();
			}
			
			private function novo(event:MouseEvent = null):void
			{
				this.currentState = "insercao";
				limpar();			
			}
			
			private function voltar(event:MouseEvent = null):void
			{
				this.currentState = "listagem";
				limpar();
				listagem.viewTable.filter();
			}
			
			private function salvarEnter(event:KeyboardEvent):void
			{		
				if (event.charCode == 13) {
					salvar();
				}
			}
			
			private function limparEnter(event:KeyboardEvent):void
			{		
				if (event.charCode == 13) {
					limpar();
				}
			}
			
			private function voltarEnter(event:KeyboardEvent):void
			{		
				if (event.charCode == 13) {
					voltar();
				}
			}
			
		]]>
	</fx:Script>
	
	<mx:states>
		<s:State name="listagem"/>
		<s:State name="edicao"/>
		<s:State name="insercao"/>
	</mx:states>
	
	<mx:VBox width="100%" horizontalAlign="center">
		<s:Label fontSize="15" fontWeight="bold" text="{resourceManager.getString('ApplicationResource','LISTA_UNIDADE')}" includeIn="listagem"/>
		<s:Label fontSize="15" fontWeight="bold" text="{resourceManager.getString('ApplicationResource','DETALHAMENTO_UNIDADE')}" excludeFrom="listagem"/>
	</mx:VBox>	
	
	<mx:VBox width="100%" horizontalAlign="left">
		<mx:Button id="newButton" includeIn="listagem"
				   label="{resourceManager.getString('ApplicationResource','NOVO')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/new.png')"
				   click="novo(event)"
				   />
	</mx:VBox>
	
	<unidade:ListagemUnidadeCadastrado id="listagem" includeIn="listagem" width="100%" height="100%" />
	
	<unidade:DadosUnidade id="dados" excludeFrom="listagem" width="100%"  />
		
	<mx:HBox width="100%" excludeFrom="listagem">
		<mx:Button id="saveButton" excludeFrom="listagem"
				   label="{resourceManager.getString('ApplicationResource','SALVAR')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/save.png')"
				   keyDown="salvarEnter(event)"
				   click="salvar(event)"
				   />
		<mx:Button id="cleanButton" excludeFrom="listagem"
				   label="{resourceManager.getString('ApplicationResource','LIMPAR')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/clean.png')"
				   keyDown="limparEnter(event)"
				   click="limpar(event)"
				   />
		<mx:Button id="voltarButton" excludeFrom="listagem"
				   label="{resourceManager.getString('ApplicationResource','VOLTAR')}"
				   height="25"
				   icon="@Embed(source='/assets/icons/back.png')"
				   keyDown="voltarEnter(event)"
				   click="voltar(event)"
				   />
	</mx:HBox>
</mx:VBox>
